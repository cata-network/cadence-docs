# 合约

------

Cadence 中的 合约是接口、结构、资源、数据（其状态）和代码（其功能）的类型定义的集合，这些类型定义位于 Flow 中帐户的 合约存储区域中。

 合约是必须在 Cadence 中定义这些类型的所有复合类型（如结构、资源、事件和接口）的地方。因此，如果未在部署的 Cadence 合约中定义，则不能存在这些类型的对象。

可以使用[授权帐户](https://docs.onflow.org/cadence/language/accounts)`contracts` 对象创建、更新和删除合约。此功能将在[下一节中介绍](https://docs.onflow.org/cadence/language/contracts/#deploying,-updating,-and-removing-contracts)

合约是类型。它们类似于复合类型，但与结构或资源的存储方式不同，并且不能像资源或结构一样用作值、 复制或移动。

 合约保留在账户的合约存储区中，只能由账户所有者使用特殊命令添加、更新或删除。

合约是使用`contract`关键字声明的。关键字后跟合约名称。

```cadence
pub contract SomeContract {
    // ...
}
```

合约不能相互嵌套。

```cadence
pub contract Invalid {

    // 无效： 合约不能嵌套在任何其他类型中。
    //
    pub contract Nested {
        // ...
    }
}
```

最简单的 合约形式之一就是具有状态字段、函数和`init`初始化字段的函数：

 

```cadence
pub contract HelloWorld {

    // 在 HelloWorld 中声明一个存储状态字段
    //
    pub let greeting: String

    // Declare a function that can be called by anyone
    // who imports the contract
    //声明一个想导入 合约的任何人都可以调用的函数
    pub fun hello(): String {
        return self.greeting
    }

    init() {
        self.greeting = "Hello World!"
    }
}
```

该合约可以部署到一个帐户并永久存在于 合约存储中。交易和其他 合约可以通过在交易或 合约定义开始时导入它们来与 合约交互。

任何人都可以`hello`通过从部署到的账户中导入合约并使用导入的对象调用 hello 函数来调用上述 合约的函数。

 

```cadence
import HelloWorld from 0x42

// 无效： 合约不知道hello来自哪里
//
log(hello())        // 错误

// 有效：使用导入的 合约对象调用hello功能
//
log(HelloWorld.hello())    // 打印 "Hello World!"

// Valid: 使用导入的 合约对象读取问候语场地。
log(HelloWorld.greeting)   // 打印 "Hello World!"

//无效： 合约创建后无法调用init函数。
//
HelloWorld.init()    // Error
```

每个账户可以有任意数量的 合约，它们可以包含任意数量的数据。这意味着 合约可以有任意数量的字段、函数和类型定义，但它们必须在 合约中，而不是另一个顶级定义。

```cadence
// 无效：顶级声明仅限于 合约或 合约接口。因此，所有这些都将无效
// 如果它们被部署到账户 合约存储因此部署将被拒绝。
//
pub resource Vault {}
pub struct Hat {}
pub fun helloWorld(): String {}
let num: Int
```

 合约的另一个重要特征是 合约中声明的资源和事件的实例只能在同一 合约中声明的函数或类型中创建/发出。

不可能在 合约之外创建资源和事件的实例。

下面的 合约定义了一个资源接口`Receiver`和一个`Vault` 实现该接口的资源。这个例子的编写方式，没有办法创建这个资源，所以它不可用。

```cadence
// 无效
pub contract FungibleToken {

    pub resource interface Receiver {

        pub balance: Int

        pub fun deposit(from: @{Receiver}) {
            pre {
                from.balance > 0:
                    "Deposit balance needs to be positive!"
            }
            post {
                self.balance == before(self.balance) + before(from.balance):
                    "Incorrect amount removed"
            }
        }
    }

    pub resource Vault: Receiver {

        //跟踪账户代币的总余额
        pub var balance: Int

        init(balance: Int) {
            self.balance = balance
        }

        // 取款从金库余额中减去金额，然后返回一个减去余额的金库对象
        pub fun withdraw(amount: Int): @Vault {
            self.balance = self.balance - amount
            return <-create Vault(balance: amount)
        }

        // 存款将保险库对象作为参数并添加它的余额到账户金库的余额，然后销毁发送的保管库，因为其余额已被消耗
        pub fun deposit(from: @{Receiver}) {
            self.balance = self.balance + from.balance
            destroy from
        }
    }
}
```

如果用户尝试运行创建该`Vault`类型实例的事务，则类型检查器将不允许这样做，因为只有`FungibleToken`  合约中的代码才能创建新的`Vault`s。

 

```cadence
import FungibleToken from 0x42

// 无效：无法在外部创建 `Vault` 类型的实例定义`Vault`的 合约
//
let newVault <- create FungibleToken.Vault(balance: 10)
```

 合约必须定义一个创建新`Vault`实例的函数， 或者使用它的`init`函数创建一个实例并将其存储在所有者的帐户存储中。

这带来了 Cadence 合约的另一个关键特征。 合约可以与其帐户`storage`和`published`对象交互以存储资源、结构和引用。他们通过使用`self.account`只能在合约中访问的特殊对象来做到这一点。

想象一下，这些是在上面的`FungibleToken` 合约中声明的。

 

```cadence
    pub fun createVault(initialBalance: Int): @Vault {
        return <-create Vault(balance: initialBalance)
    }

    init(balance: Int) {
        let vault <- create Vault(balance: 1000)
        self.account.save(<-vault, to: /storage/initialVault)
    }
```

现在，任何账户都可以调用`createVault` 合约中声明的函数来创建`Vault`对象。或者所有者可以`withdraw`自己调用该函数`Vault`将新的保险库发送给其他人。

 

```cadence
import FungibleToken from 0x42

// 有效：通过调用 合约的创建一个 `Vault` 类型的实例`createVault` 函数。
//
let newVault <- create FungibleToken.createVault(initialBalance: 10)
```

 合约具有隐式字段`let account: Account`，它也是部署 合约的帐户。这使 合约能够读取和写入帐户的存储。

## 部署、更新和删除合约

为了在 Cadence 中使用 合约，需要将其部署到帐户中。可以通过`contracts`对象访问帐户的已部署 合约。

### 已部署的 合约

账户存储“部署的 合约”，即 合约的代码：

 

```cadence
struct DeployedContract {
    let name: String
    let code: [UInt8]
}
```

注意，这不是可以通过导入获取的 合约实例。

### 部署新 合约

可以使用以下`add`功能将新 合约部署到帐户：

 

```cadence
fun add(
    name: String,
    code: [UInt8],
    ... contractInitializerArguments
): DeployedContract
```

将给定的合约添加到帐户中。

该`code`参数是源代码的 UTF-8 编码表示。代码必须只包含一个 合约或 合约接口，并且必须与`name`参数同名。

给定的所有附加参数都进一步传递给正在部署的 合约的初始值设定项。

如果账户中已经存在具有给定名称的 合约/ 合约接口，如果给定的代码没有准确声明一个 合约或 合约接口，或者给定的名称与 合约/ 合约接口声明的名称不匹配，则失败代码。

返回[部署的合约](https://docs.onflow.org/cadence/language/contracts/#deployed-contracts)。

例如，假设应部署以下 合约代码：

 

```cadence
pub contract Test {
    pub let message: String

    init(message: String) {
        self.message = message
    }
}
```

 合约可以部署如下：

 

```cadence
// 使用内置函数`decodeHex`将十六进制编码的源代码解码为字节数组。（省略号...表示字符串的剩余部分）
//
let code = "70756220636f6e...".decodeHex()

// `code` 的类型为 `[UInt8]`

let signer: Account = ...
signer.contracts.add(
    name: "Test",
    code: code,
    message: "I'm a new contract in an existing account"
)
```

### 更新已部署的合约

> 🚧 状态：更新合约是**实验性的**。
>
> 更新 合约目前仅限于保持数据一致性。在[合约可更新性](https://docs.onflow.org/cadence/language/contract-updatability)部分阅读有关更新合约的有效更改和限制的更多详细信息 。

可以使用以下`update__experimental`函数更新已部署的 合约：

 

```cadence
fun update__experimental(name: String, code: [UInt8]): DeployedContract
```

更新账户中 合约/ 合约接口的代码。

该`code`参数是源代码的 UTF-8 编码表示。代码必须只包含一个合约或合约接口，并且必须与`name`参数同名。

无需再次运行该合约/合约接口的初始化。全局状态中合约实例保持原样。

如果账户中不存在具有给定名称的 合约/ 合约接口，如果给定的代码没有准确声明一个合约或合约接口，或者给定的名称与代码中的合约/ 合约接口声明的名称不匹配，则失败返回更新合约的[部署](https://docs.onflow.org/cadence/language/contracts/#deployed-contracts)合约。

例如，假设一个名为的合约`Test`已经部署到账户中，并且应该使用以下 合约代码进行更新：

```cadence
pub contract Test {
    pub let message: String

    init(message: String) {
        self.message = message
    }
}
```

 合约可以更新如下：

```cadence
// 使用内置函数`decodeHex`将十六进制编码的源代码解码为字节数组。（省略号...表示字符串的剩余部分）
//
let code = "70756220636f6e...".decodeHex()

// `code` 的类型为 `[UInt8]`

let signer: Account = ...
signer.contracts.update__experimental(name: "Test", code: code)
```

### 获取已部署的合约

可以使用以下`get`函数从帐户中获取已部署的合约：

```cadence
fun get(name: String): DeployedContract?
```

返回在帐户中具有给定名称的合约/合约接口的已[部署合约](https://docs.onflow.org/cadence/language/contracts/#deployed-contracts)（如果有）。

`nil`如果帐户中不存在具有给定名称的 合约/ 合约接口，则 返回。

例如，假设一个名为的 合约`Test`部署到一个账户，可以按如下方式检索该合约：

```cadence
let signer: Account = ...
let contract = signer.contracts.get(name: "Test")
```

### 删除已部署的 合约

可以使用以下`remove`函数从帐户中删除已部署的 合约：

 

```cadence
fun remove(name: String): DeployedContract?
```

从具有给定名称的帐户中删除合约/合约接口（如果有）。

返回已删除的已[部署合约](https://docs.onflow.org/cadence/language/contracts/#deployed-contracts)（如果有）。

如果帐户中不存在具有给定名称的 合约/ 合约接口，则 返回`nil`。

例如，假设一个名为的 合约`Test`部署到一个账户，可以移除该合约：

```cadence
let signer: Account = ...
let contract = signer.contracts.remove(name: "Test")
```

##  合约接口

像复合类型一样， 合约可以有接口来指定关于它们的行为、它们的类型和它们的类型的行为的规则。

合约接口必须全局声明。声明不能嵌套在其他类型中。

如果协定接口声明了具体类型，则它的实现也必须声明符合类型要求的相同具体类型。

如果 合约接口声明了接口类型，则实现 合约不必也定义该接口。他们可以通过说来引用该嵌套接口`{ContractInterfaceName}.{NestedInterfaceName}`

```cadence
// 声明一个 合约接口，声明一个接口和一个资源并需要在合约实现中实现该接口。
//
pub contract interface InterfaceExample {

    // 实现不需要声明 this，应将其称为 InterfaceExample.NestedInterface
    //
    pub resource interface NestedInterface {}

    // 实现必须声明这种类型
    //
    pub resource Composite: NestedInterface {}
}

pub contract ExampleContract: InterfaceExample {

    //  合约不需要重新声明`NestedInterface`接口，因为它已经在 合约接口中声明了
    // 资源必须使用名称来引用资源接口访问它的 合约接口
    //
    pub resource Composite: InterfaceExample.NestedInterface {
    }
}
```